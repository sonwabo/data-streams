buildscript {
  ext {
    buildNumber = System.getenv('BITBUCKET_BUILD_NUMBER') ?: 'LOCAL-' + new Date().toString()
    logger.lifecycle "BUILD_NUMBER=$buildNumber"
    springBootVersion = '2.0.6.RELEASE'
    springFoxVersion = '3.0.0-SNAPSHOT'
    platformBomVersion = 'Cairo-SR5'
  }
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
    classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.3'
  }
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply from: 'release.gradle'

group = 'uk.co.omniolytics'

sourceCompatibility = 1.8

repositories {
  mavenLocal()
  maven {
    credentials {
      username rootProject.findProperty('jumpCoMavenUser') ?: System.getenv('MAVEN_USERNAME')
      password rootProject.findProperty('jumpCoMavenPassword') ?: System.getenv('MAVEN_PASSWORD')
    }
    url 'https://jumpco.jfrog.io/jumpco/libs'
  }
  jcenter()
  mavenCentral()
  maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}


dependencies {
  if (rootProject.findProject(':omnio-model')) {
    implementation(project(':omnio-model'))
  } else {
    implementation('uk.co.omniolytics:omnio-model:1.0.5')
  }
  implementation('org.slf4j:slf4j-api')
  implementation('org.slf4j:slf4j-ext')
  implementation('javax.validation:validation-api')
  implementation('com.cloudant:cloudant-client')
  implementation('com.squareup.okhttp3:okhttp-urlconnection')
  implementation('org.springframework:spring-core')
  implementation('org.springframework:spring-context')
  implementation('org.springframework:spring-beans')
  implementation('org.springframework.data:spring-data-commons')
  implementation('joda-time:joda-time')
  implementation('com.fasterxml.jackson.datatype:jackson-datatype-joda')
  implementation('com.fasterxml.jackson.core:jackson-databind')
  compileOnly('org.projectlombok:lombok')
  testImplementation('junit:junit')
  testImplementation('org.assertj:assertj-core:3.11.1')
  testImplementation('org.skyscreamer:jsonassert')
  testImplementation('log4j:log4j')
  testImplementation('org.slf4j:slf4j-log4j12')
  testImplementation('org.springframework:spring-test')
  testImplementation('commons-io:commons-io')
}

dependencyManagement {
  imports {
    mavenBom 'io.spring.platform:platform-bom:' + platformBomVersion
  }
  dependencies {
    dependency('org.slf4j:slf4j-api:1.7.25')
    dependency('org.slf4j:slf4j-ext:1.7.25')
    dependency('org.projectlombok:lombok:1.18.4')
    dependency('javax.validation:validation-api:2.0.1.Final')
    dependency('io.springfox:springfox-swagger2:' + springFoxVersion)
    dependency('com.cloudant:cloudant-client:2.13.1')
    dependency('com.squareup.okhttp3:okhttp-urlconnection:3.8.1')
  }
}

jar {
  manifest {
    attributes 'Implementation-Version': project.version, 'Build-Number': buildNumber
  }
}
